//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2017, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

include "apex/engine/chat/alias.galaxy"
include "apex/engine/chat/macro.galaxy"
include "apex/engine/chat/variables.galaxy"

const string libapx_CHAT_COMMAND_DATA_TABLE_PREFIX = "libapx_command_";

// libapx_arg
// ===============
// Returns the string argument value at the index.
// Wrapper for StringWord in order to appear more descriptive in commands.
string libapx_arg(string argv, int index) {
	return StringWord(argv, index);
}

// function signature for Apex chat commands
void libapx_command_signature(int user, unitgroup u, string argv);

// funcref type for Apex commands
typedef funcref<libapx_command_signature> libapx_command_type;

// Table of all Apex commands:
// ===========================
// Commands are created and hardcoded/registered as an entry in the table.
// Global data table maps a function name to an index in this array:
//
//   DataTable index -> commands[index] -> command funcref
//
// Though this is an annoying double leap (map -> array -> func), easier
// to reference a command in the table instead of creating an if-elif cascade
// to match a function string name to its function reference
//
// Array length hardcoded such that num of commands registered should never
// reach command table size. If it does, increase this array size.
libapx_command_type[100] libapx_command_table;

// Counter for the next free array entry in the libapx_command_table array
int libapx_command_table_counter = 0;

// libapx_chat_command_create
// ==================
// Adds a command function to the global array of commands.
// Use data table to map command name (string) to the array index.
void libapx_chat_command_create(string command, libapx_command_type func) {
	DataTableSetInt(true, libapx_CHAT_COMMAND_DATA_TABLE_PREFIX + command, libapx_command_table_counter);
	libapx_command_table[libapx_command_table_counter] = func;
	libapx_command_table_counter += 1;

	// add command to list of commands
	libapx_settings.commands = libapx_settings.commands +  ", " + command;
}

// Max iterations for number of aliases and macros replaced by parser
// per parser function call. This is to break from infinite loops caused
// by recursive aliases or macros.
const int libapx_PARSE_LIMIT = 4000;

// libapx_chat_parse
// =================
// parses a chat message string for commands (each command separated by ";" )
// built-in commands are "_alias", "macro", and "var" which affect parsing
// hence these are treated separately than all other commands
void libapx_chat_parse(string msg, int player, unitgroup target) {
	string current = msg;
	string current_alias_subbed;
	string cmd_key;
	int count = 0;
	int semicolon_index;
	int next_semicolon_index;
	libapx_command_type func; // command ref test
	libapx_player[player].context = target;
	// manual interrupt flag to stop parsing, intialize as false
	libapx_player[player].done_parsing = false;

	while ( msg != "" && count < libapx_PARSE_LIMIT) {
		// if player done parsing flag set to true, interrupt and stop parsing
		if ( libapx_player[player].done_parsing == true ) {
			return;
		}

		// update player's chat_msg current cache
		libapx_player[player].chat_msg = msg;

		// get a chunk of the msg by finding ";" separater
		// -> to allow players to still use ";" in chat, "\;" is parsed as an escape sequence
		// -> "\;" will print out a ";" in the chat, so we must find "\;" in a message
		semicolon_index = StringFind(msg, ";", c_stringNoCase);
		while( true ) {

			if( semicolon_index ) {
				// if this is an escaped semicolon "\;", find the next semicolon
				if( StringSub(msg, semicolon_index - 1, semicolon_index - 1) == "\\" ) {
					next_semicolon_index = StringFind( StringSub( msg, semicolon_index + 1, StringLength(msg) ), ";", c_stringNoCase );
					if( next_semicolon_index > 0 ) {
						// must add because next_semicolon_index is relative to a substring
						semicolon_index = semicolon_index + next_semicolon_index;
						continue;
					}
					else { // at end of msg
						current = msg;
						msg = "";
						break;
					}
				}
				else {
					current = libapx_string_trim( StringSub(msg, 1, semicolon_index - 1) );
					msg = StringSub( msg, semicolon_index + 1, StringLength(msg) );
					break;
				}
			}
			else { // at end of msg
				current = msg;
				msg = "";
				break;
			}

		}
		// after getting current, replace escape sequence for semicolon: "\;" -> ";"
		current = StringReplaceWord(current, "\\;", ";", c_stringReplaceAll, false);
		// go no further with parsing if current string is empty
		// (this occurs if player types a string of ";;;;;")
		if( current == "" ) {
			continue;
		}

		// check if performing reserved keyword commands (alias, macro, var)
		if ( StringEqual( StringWord(current, 1), "_alias", c_stringNoCase) ) {

			// returns true if using "alias create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( libapx_alias_command_redirect(current, player) ) {
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "macro", c_stringNoCase) ) {
			// returns true if using "macro create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( libapx_macro_command_redirect(current, player) ) {
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "var", c_stringNoCase) ) {
			// TODO: ADD VARIABLE SYSTEM
		}

		// replace aliases (returns true if aliases):
		// if aliases subbed, affix to front of msg and reparse

		current_alias_subbed = libapx_alias_sub(current, player, 0);
		if( (current_alias_subbed != current) ) {
			msg = current_alias_subbed + " ;" + msg;
			count += 1;
			continue;
		}

		// if macro exists, append evaluated macro to msg and parse
		if( BankKeyExists( libapx_player[player].bank_storage, libapx_BANK_SECTION_MACRO, StringWord(current, 1) ) ) {
			msg = libapx_macro(current, player) + ";" + msg;
			count += 1;
			continue;
		}

		// TODO: replace variables


		// run command (if exists and permissions for player valid)
		cmd_key = libapx_CHAT_COMMAND_DATA_TABLE_PREFIX + StringCase(StringWord(current, 1), false);
		if ( DataTableValueExists(true, cmd_key) ) {
			func = libapx_command_table[ DataTableGetInt(true, cmd_key) ];
			target = libapx_player[player].context;
			func( player, target, current );
		}
		else if ( !libapx_player[player].muted ) {
			libapx_chat_print_OOC(player, current);
		}
	}

	// reached parse limit
	if( count == libapx_PARSE_LIMIT ) {
		libapx_print_error(player, "Reached parse limit (recursive alias or recursive macro?)");
	}

}
